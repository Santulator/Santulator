/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */
import org.apache.tools.ant.filters.*

apply plugin: 'application'

mainClassName = "io.github.santulator.gui.main.SantulatorGuiExecutable"

applicationName = "Santulator"

ext.buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss')

processResources {
    filesMatching('properties/*.properties') {
        filter ReplaceTokens, tokens: [
                'build.version': project.property("version"),
                'build.timestamp': project.buildTimestamp
        ]
    }
}

jar {
    from(buildDir) {
        include 'build-info.properties'
    }
}

dependencies {
    compile project(':core')
    compile 'com.gluonhq:ignite-guice:1.0.2'

    // This is required for the conditional logic in the logback setup
    runtime 'org.codehaus.janino:janino:3.0.11'

    testCompile project(path: ':core', configuration: 'tests')

    testRuntime 'commons-io:commons-io:2.6'

    testCompile 'org.testfx:testfx-core:4.0.15-alpha'
    if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
        compile 'org.controlsfx:controlsfx:8.40.14'

        testRuntime 'org.testfx:openjfx-monocle:8u76-b04'
    } else {
        compile 'org.controlsfx:controlsfx:9.0.0'

        testRuntime 'org.testfx:openjfx-monocle:jdk-11+26'
    }
}

test {
    if (project.hasProperty("skipGuiTests")) {
        exclude 'io/github/santulator/gui/main/GuiTest*'
    }
    if (!project.hasProperty("noHeadless")) {
        jvmArgs "-Dheadless=true"
    }
}

if (JavaVersion.current() == JavaVersion.VERSION_11) {
    def platform
    if (operatingSystem.isWindows()) {
        platform = 'win'
    } else if (operatingSystem.isLinux()) {
        platform = 'linux'
    } else if (operatingSystem.isMacOsX()) {
        platform = 'mac'
    }

    configurations {
        javafxCompile
    }

    dependencies {
        javafxCompile "org.openjfx:javafx-base:11.0.1:${platform}"
        javafxCompile "org.openjfx:javafx-graphics:11.0.1:${platform}"
        javafxCompile "org.openjfx:javafx-controls:11.0.1:${platform}"
        javafxCompile "org.openjfx:javafx-fxml:11.0.1:${platform}"

        compileOnly configurations.javafxCompile
        pmd configurations.javafxCompile
    }

    tasks.withType(JavaCompile) {
        options.with {
            compilerArgs += [
                '--module-path', configurations.javafxCompile.asPath,
                '--add-modules', 'javafx.controls'
            ]
        }
    }

    [run, test].each {
        it.jvmArgs += [
            '--module-path', configurations.javafxCompile.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml',
            '--add-exports', 'javafx.base/com.sun.javafx.runtime=ALL-UNNAMED',
            '--add-exports', 'javafx.graphics/com.sun.javafx.css=ALL-UNNAMED'
        ]
    }

    test {
        jvmArgs += [
            '--add-opens', 'javafx.graphics/com.sun.glass.ui=ALL-UNNAMED',
            '--add-exports', 'javafx.graphics/com.sun.javafx.application=ALL-UNNAMED'
        ]
    }
}
