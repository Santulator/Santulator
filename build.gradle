/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */

import org.gradle.internal.os.OperatingSystem

plugins {
    id "base"
    id "com.github.ben-manes.versions" version "0.51.0"
    id "net.researchgate.release" version "3.0.2"
    id "org.sonarqube" version "3.3"
    id "com.github.spotbugs" version "5.0.14" apply false
}

ext {
    operatingSystem = OperatingSystem.current()
}

allprojects {
    group 'io.github.santulator'
}

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = '8.11.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'jacoco'

    sourceCompatibility = 17
    targetCompatibility = 17

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
        runtimeOnly 'ch.qos.logback:logback-classic:1.5.12'
    }

    test {
        useJUnitPlatform()
    }

    pmd {
        toolVersion = '6.55.0'
        ruleSetFiles = files("$rootProject.projectDir/config/pmd/ruleset.xml")
        ruleSets = []
    }

    checkstyle {
        toolVersion = '10.11.0'
        configFile = file("$rootProject.projectDir/config/checkstyle/checkstyle.xml")
    }

    spotbugs {
        toolVersion = '4.7.3'
        effort = 'max'
        reportLevel = 'medium'
        excludeFilter = file("$rootProject.projectDir/config/spotbugs/excludeFilter.xml")
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacoco {
        toolVersion = '0.8.10'
    }

    jacocoTestReport {
        reports {
            xml.required = true
            xml.outputLocation = file("${buildDir}/reports/jacoco/report.xml")
        }
    }

    [compileJava, compileTestJava].each { task ->
        task.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
    }
}
